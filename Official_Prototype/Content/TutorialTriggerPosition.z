class TutorialTriggerPosition : ZilchComponent
{
    //Position of the object when not seen
    [Property]
    var NotActivePos:Real3;
    //Position of the object when seen
    [Property]
    var ActivePos:Real3;
    //Sets the correct position on initialize
    [Property]
    var StartActive:Boolean = false;
    
    function Initialize(init : CogInitializer)
    {
        if(this.StartActive)
        {
            //Moves this owner to the correct spot
            this.SetToStart();
        }
        else
        {
            //Moves this owner out of the way
            this.SetToEnd();
        }
        
        Zero.Connect(this.Space, Events.TriggerEvent, this.OnTrigger);
    }
    
    
    //Puts this object in the correct position when triggered specifically by a trigger event
    function OnTrigger(event : TriggerEvent)
    {
        if(this.Owner == event.CogToTrigger1 || this.Owner.Name == event.NameToTrigger)
        {
            //Sets the location based on the trigger 1
            if(event.StartTrigger1)
            {
                this.SetToStart();
            }
            else
            {
                this.SetToEnd();
            }
        }
        else if(this.Owner == event.CogToTrigger2)
        {
            //Sets the location based on the trigger 2
            if(event.StartTrigger2)
            {
                this.SetToStart();
            }
            else
            {
                this.SetToEnd();
            }
        }
    }
    
    //Sets the object to the start position and visible
    function SetToStart()
    {
        this.Owner.Transform.Translation = this.ActivePos;
        
        if(this.Owner.Sprite != null)
        {
            this.Owner.Sprite.Visible = true;
        }
        
        if(this.Owner.SphericalParticleEmitter != null)
        {
            this.Owner.SphericalParticleEmitter.Active = true;
        }
        
        if(this.Owner.BoxCollider != null)
        {
            this.Owner.BoxCollider.SendsEvents = true;
        }
    }
    
    //Sets the object to the end position and not visible
    function SetToEnd()
    {
        this.Owner.Transform.Translation = this.NotActivePos;
        
        if(this.Owner.Sprite != null)
        {
            this.Owner.Sprite.Visible = false;
        }
        
        if(this.Owner.SphericalParticleEmitter != null)
        {
            this.Owner.SphericalParticleEmitter.Active = false;
        }
        
        if(this.Owner.BoxCollider != null)
        {
            this.Owner.BoxCollider.SendsEvents = false;
        }
    }
}
